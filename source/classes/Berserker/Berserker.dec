const int count = 2;

ACTOR BerserkerClass : PlayerPawn
{
	Health 200
	Mass 300
	Radius 24
	Height 64
	Speed 1
	PainChance 255
	Player.DisplayName "Berserker"
	Player.Viewheight 56
	Player.SoundClass "berserker"
	Player.StartItem "BerserkerFists", 1
	Player.Startitem "greenarmor", 1
	player.startitem "isSeeker", 1
	player.startitem "BerserkerFirstCooldown", 999
	player.startitem "BerserkerSecondCooldown", 999
	Player.ForwardMove 0.50, 1.25
	Player.SideMove 0.50, 1.25
	Player.CrouchSprite "BSKR"
	Player.JumpZ 12
	limitedtoteam 1
	damagefactor "rocket", 0
	var int user_count;
	+NOSKIN
	States
	{
		Spawn:
			TNT1 A 0 A_PlaySound("dsjugstp")
			BSKR A 35
			TNT1 A 0 ACS_NamedExecute("regenHealth",0,0)
		Spawn2:
			TNT1 A 0 A_JumpIf(momz != 0, 3)
			TNT1 A 0 A_SetUserVar("user_count", 0)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_SetUserVar("user_count", user_count+1)
			BSKR A 1 A_JumpIf(user_count == count, "Jump")
			Loop
		See:
			//TNT1 A 0 A_Quake(1, 20, 0, 32, "") //sorta fucks with the sound and players view
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_JumpIf(momz != 0, 3)
			TNT1 A 0 A_SetUserVar("user_count", 0)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_SetUserVar("user_count", user_count+1)
			TNT1 A 0 A_JumpIf(user_count == count, "Jump")
			TNT1 A 0 A_PlaySound("dsjugstp")
			BSKR A 8
			TNT1 A 0 A_JumpIf(momz != 0, 3)
			TNT1 A 0 A_SetUserVar("user_count", 0)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_SetUserVar("user_count", user_count+1)
			BSKR B 8 A_JumpIf(user_count == count, "Jump")
			//TNT1 A 0 A_Quake(1, 20, 0, 32, "")
			TNT1 A 0 A_JumpIf(momz != 0, 3)
			TNT1 A 0 A_SetUserVar("user_count", 0)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_SetUserVar("user_count", user_count+1)
			TNT1 A 0 A_JumpIf(user_count == count, "Jump")
			TNT1 A 0 A_PlaySound("dsjugstp")
			BSKR C 8
			TNT1 A 0 A_JumpIf(momz != 0, 3)
			TNT1 A 0 A_SetUserVar("user_count", 0)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_SetUserVar("user_count", user_count+1)
			BSKR D 8 A_JumpIf(user_count == count, "Jump")
			Goto See+1
		Jump:
			TNT1 A 0 A_SetUserVar("user_count", 0)
			BSKR B 1
			TNT1 A 0 A_CheckFloor("Spawn")
			Goto Jump
		Melee:
			Goto Missile
		Missile:
			TNT1 A 0 A_Jump(128, 5)
			BSKR EF 3
			BSKR G 4
			BSKR H 6
			Goto Spawn2
			BSKR IJ 8
			Goto Spawn2
		Pain:
			TNT1 A 0 A_Pain
			Goto Spawn2
		Death:
			BSKR K 10
			BSKR L 10 A_PlayerScream
			BSKR M 10 A_NoBlocking
			BSKR NOP 10
			BSKR Q -1
			Stop
		XDeath:
			BSKR K 10
			BSKR L 10 A_PlayerScream
			BSKR M 10 A_NoBlocking
			BSKR NOP 10
			BSKR Q -1
			Stop
	}
}

ACTOR BerserkerFists : Weapon
{
	+WEAPON.MELEEWEAPON
	Obituary "%o felt %k's berserker rage!!!!!!!!!!!!!!!!!!!!!"
	States
	{
		Select:
			FPCH A 1 A_Raise
			Loop
		Deselect:
			FPCH A 1 A_Lower
			Loop
		Ready:
			FPCH A 1 A_WeaponReady
			Loop
		Fire:
			FPCH E 0 A_JumpIfInventory("FalconPunch",1,2)
			FPCH E 0 A_PlaysoundEx("BerserkerTaunt","SoundSlot6",0,0)
			FPCH E 0 A_GiveInventory("FalconPunch",1)
			FPCH E 0 A_Jump(127, 15)
			FPCH E 1 A_PlaySound("PUNCHMIS")
			FPCH E 1 Offset(-20, 40)
			FPCH E 1 Offset(-40, 40)
			FPCH E 1 Offset(-80, 40)
			FPCH E 1 Offset(-120, 40)
			FPCH E 1 Offset(-160, 40) A_CustomPunch(500,0,0,"GauntletPuff",96)
			FPCH E 1 Offset(-170, 50)
			FPCH E 1 Offset(-180, 60)
			FPCH E 1 Offset(-190, 70) A_PlaySound("dsjugstp")
			FPCH E 1 Offset(-200, 80)
			FPCH E 1 Offset(-210, 100) 
			FPCH E 1 Offset(-220, 120)
			FPCH E 1 Offset(-230, 140) A_Refire
			FPCH A 0 A_TakeInventory("FalconPunch",1)
			Goto Ready
			FPCH F 1 A_PlaySound("PUNCHMIS")
			FPCH F 1 Offset(20, 40)
			FPCH F 1 Offset(40, 40)
			FPCH F 1 Offset(80, 40)
			FPCH F 1 Offset(120, 40)
			FPCH F 1 Offset(160, 40) A_CustomPunch(500, 0, 0, "GauntletPuff", 96)
			FPCH F 1 Offset(170, 50)
			FPCH F 1 Offset(180, 60)
			FPCH F 1 Offset(190, 70)  A_PlaySound("dsjugstp")
			FPCH F 1 Offset(200, 80)
			FPCH F 1 Offset(210, 100)
			FPCH F 1 Offset(220, 120) 
			FPCH F 1 Offset(230, 140) A_Refire
			FPCH A 0 A_TakeInventory("FalconPunch",1)
			Goto Ready
	}
}

ACTOR BerserkerShotgun : Weapon
{
    Weapon.Kickback 100
    Weapon.AmmoUse 2
    Weapon.AmmoGive 8
    Weapon.AmmoType "Shell"
    Obituary "$OB_MPSSHOTGUN"
    States
    {
    Ready:
        BSSG A 1 A_WeaponReady
        Loop
    Deselect:
        BSSG A 0 A_TakeInventory("BerserkerShotgun", 1, AAPTR_TARGET)
        BSSG A 1 A_Lower
        Loop
    Select:
        BSSG A 0 A_SpawnItemEx("FloatingShotgun", 0, 48, 40, 0, 0, 0, angle, SXF_CLIENTSIDE)
        BSSG A 1 A_Raise
        Goto Select+1
    Fire:
        BSSG A 0 A_GunFlash
        BSSG A 0 A_PlaySound("Berserker/ShotgunFire", CHAN_WEAPON)
        BSSG A 8 A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff")
        BSSG A 4
        BSSG D 3 A_CheckReload
        BSSG E 3
        BSSG F 3 A_PlaySound("Berserker/ShotgunOpen", CHAN_WEAPON)
        BSSG GHIJK 3
        BSSG L 3 A_PlaySound("Berserker/ShotgunLoad", CHAN_WEAPON)
        BSSG MNOPQRS 3
        BSSG S 3 A_PlaySound("Berserker/ShotgunClose", CHAN_WEAPON)
        BSSG A 3
        Goto Ready
    Flash:
        BSSG B 4 Bright A_Light1
        BSSG C 4 Bright A_Light2
        Goto LightDone
    Spawn:
        SGN2 A -1
        Stop
    }
}

ACTOR FloatingShotgun
{
    // Since we can't exclude visibility, this must be done.
    VisibleToPlayerClass "BansheeClass", "BlazerClass", "GuardianClass", "HunterClass", "IllusionistClass", "PolymorphistClass", "RocketeerClass", "ScouterClass", "TacticianClass", "TeleporterClass", "TerminatorClass"
    Scale 0.65
    +NOINTERACTION
    States
    {
    Spawn:
        BFSG A 1 A_Warp(AAPTR_TARGET, 48, 0, 40, 0, WARPF_STOP|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
        TNT1 AAA 0 A_JumpIfInTargetInventory("BerserkerShotgun", 1, "Spawn")
        Stop
    }
}

ACTOR BerserkerHammer : Weapon
{
  Obituary "%o fell victim to %k's hammer time!!!!!!!!!!!!!!!!!!!!!"
  States
  {
  Spawn:
    TNT1 A -1
    Stop
  Select:
    TNT1 A 1 A_Raise
    Loop
  Deselect:
    TNT1 A 1 A_Lower
    Loop
  Ready:
    TNT1 A 0 A_WeaponReady
    Goto Fire
  Fire:
	FHMR B 3 OffSet(5, 0)
	FHMR C 3 OffSet(5, 0)
	FHMR D 2 OffSet(5, 0)
	TNT1 A 0 A_PlaySound("Berserker/Abilities/HammerThrow")
	TNT1 A 0 A_FireCustomMissile("HammerMissile", 0, 0, 0, 0)
	FHMR E 2 OffSet(5, 0)	
	FHMR E 4 OffSet(5, 150) 
	FHMR E 0 A_TakeInventory("ThrowingAxe",1)
	Goto Ready
  }
}

ACTOR GauntletPuff : BulletPuff
{
	AttackSound "PUNCHWAL"
	States
	{
	Spawn:
	Melee:
		TNT1 A 0 A_PlaySound("PUNCHHIT")
		Stop
	Crash:
		FHFX STUVW 2
		Stop
	
	}
}

ACTOR FalconPunch : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 1
	States
	{
	Spawn:
	TNT1 A -1
	Loop
	}
}

ACTOR HammerMissile
{
  Speed 50
  Radius 14
  Height 20
  Damage 50
  Projectile
  DeathSound "Berserker/Abilities/HammerHit"
  Obituary "fell victim to hammer time"
  States
  {
  Spawn:
    FHFX A 2 Bright
    FHFX B 2 Bright A_PlaySound("Berserker/Abilities/HammerIdle")
    FHFX CDEFGH 2 Bright
    Loop
  Death:
    FHFX I 3 Bright A_SetTranslucent(1, 1)
    FHFX J 3 Bright
    FHFX K 3 Bright A_Explode(128, 128, 0)
    FHFX LM 3 Bright
    FHFX N 3
    FHFX OPQR 3 Bright
    Stop
  }
}

ACTOR ThrowingAxe : Inventory
{
	Inventory.MaxAmount 1
}