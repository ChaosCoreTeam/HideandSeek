ACTOR GuardianArmor : BlueArmor
{
	Armor.SaveAmount 300
	Inventory.Icon "GARMA0"
}

ACTOR GuardianShieldArmor : GuardianArmor
{
	Armor.SavePercent 75
}

Actor GuardianMegaArmor : GuardianArmor
{
	DamageFactor 0.5
	Armor.SavePercent 200
	Inventory.Icon "GSARA0"
}

ACTOR GuardianClass : PlayerPawn
{
	Speed 1
	Health 200
	Radius 16
	Height 56
	Mass 100
	PainChance 255
	Player.ColorRange 112, 127
	Player.DisplayName "Guardian"
	Player.SoundClass "Guardian"
	Player.CrouchSprite "RKTC"
	player.startitem "BlueShield", 1
	player.startitem "RedShield", 1
	player.startitem "GreenShield", 1
	player.startitem "lowVanishingPool", 45
	player.startitem "GuardianArmor", 1
	player.startitem "GuardianPower", 600
	player.startitem "GuardianFirstCooldown", 999
	player.startitem "GuardianSecondCooldown", 999
	player.startitem "HealthNumSpawner2", 1
	player.startitem "isHider", 1
	Player.JumpZ 0
	Species "Hider"
	var int user_chargecount;
	damagefactor "rocket", 0
	limitedtoteam 0
	+NOSKIN
	States
	{
	Spawn:
		AGRD A 0
		TNT1 A 0 SetPlayerProperty(0,1,2)
		AGRD A 35
		AGRD A 0 A_JumpIf (health>=50, 2)
		AGRD A 0 ACS_NamedExecute("regenHealth",0,0)
		AGRD A 0 A_JumpIfInventory("GuardianPower", 500, "Spawn")
		AGRD A 35
		AGRD A 0 ACS_NamedExecute("rechargeWeapon",0,2)
	Loop
	See:
		AGRD ABCD 6 
	Goto Spawn
	Missile:
		AGRD E 12
	Goto Spawn
	Melee:
		AGRD O 6 BRIGHT
	Goto Missile
	Pain:
		AGRD G 4 
		AGRD G 4 A_Pain
	Goto Spawn
	Death:
		AGRD H 10
		AGRD I 10 A_PlayerScream
		AGRD J 10 A_NoBlocking
		AGRD KLM 10
		AGRD N -1
	Stop
	XDeath:
		AGRD O 0 A_Stop
		AGRD O 5 A_SpawnItemEx ("HumanGibs",0,0,0,0,0,0,0,128)
		AGRD O 0 A_SkullPop("BlueHeadGib")
		AGRD P 5 A_XScream
		AGRD Q 5 A_NoBlocking
		AGRD RSTUV 5
		AGRD W -1
    Stop
	FadeIn:
		AGRD A 1 A_SetTranslucent(0.25, 1)
	Goto Spawn
	FadeOut:
		AGRD A 1 A_SetTranslucent(0.15, 1)
	Goto Spawn
	Visible:
		AGRD A 1 A_SetTranslucent(1, 0)
	Goto Spawn
	ArmorDamage:
		AGRD A 1 A_PlaySound("Guardian/Abilities/ArmorHit", 4096, 1.0, false, ATTN_NORM)
	Goto Spawn
	}
}