#library "VANISH"
#import "DEFINITIONS.acs"
#include "zcommon.acs"

int buttons;
int nobuttons;

Script "vanishingSetup" (void) 
{
	int pNum = PlayerNumber() + PLAYER_BASE_TID;
	
	if(PlayerClass(PlayerNumber()) < SEEKER_CLASS_START) //Hider check
	{
		while (isVanished(pNum) == true)
		{
			switch (PlayerClass(PlayerNumber()))
			{
				case CLASS_BANSHEE:
					if (CheckActorInventory(pNum, "Vanishing") > 0)
					{
						TakeActorInventory(pNum, "Vanishing", 1);
					}
					break;
				case CLASS_BLAZER:
					if (CheckActorInventory(pNum, "lowVanishingPool") > 0)
					{
						TakeActorInventory(pNum, "lowVanishingPool", 1);
					}
					break;
				case CLASS_GUARDIAN:
					if (CheckActorInventory(pNum, "medVanishingPool") > 0)
					{
						TakeActorInventory(pNum, "medVanishingPool", 1);
					}
				break;
				case CLASS_ILLUSIONIST:
					if (CheckActorInventory(pNum, "lowVanishingPool") > 0)
					{
						TakeActorInventory(pNum, "lowVanishingPool", 1);
					}
				break;
				case CLASS_ROCKETEER:
					if (CheckActorInventory(pNum, "medVanishingPool") > 0)
					{
						TakeActorInventory(pNum, "medVanishingPool", 1);
					}
				break;
				case CLASS_TELEPORTER:
					if (CheckActorInventory(pNum, "lowVanishingPool") > 0)
					{
						TakeActorInventory(pNum, "lowVanishingPool", 1);
					}
				break;
			}
			delay(15);
		}
	}
}

script "HNS_Vanish" (void) NET
{
	if(PlayerClass(PlayerNumber()) >= SEEKER_CLASS_START) terminate;
	
	if(isVanished(PlayerNumber()) == 1)
	{
		ACS_NamedExecuteAlways("HNS_Visible", 0); 
	}
	
	if(hasVanish(PlayerNumber()) == 0)
	{
		ACS_NamedExecuteAlways("HNS_NoVanish", 0);
		terminate;
	}
	
	PlaySound (PlayerNumber()+ 1000, "VanishOn", 5);
	HudMessage(s:"You are now vanished!"; HUDMSG_FADEOUT, 7, -1, 1.5, 0.65, 3.5, 7.0);
	ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
}

script "HNS_Visible" (void) NET
{
	if(PlayerClass(PlayerNumber()) >= SEEKER_CLASS_START) terminate;
	
	if(isVanished(PlayerNumber()) == 0)
	{
		ACS_NamedExecuteAlways("HNS_Vanish", 0); 
	}
	
	PlaySound (PlayerNumber()+ 1000, "VanishOff");
	HudMessage(s:"You are now visible!"; HUDMSG_FADEOUT, 7, -1, 1.5, 0.65, 3.5, 7.0);
	ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 10);
}

script "HNS_NoVanish" (void) CLIENTSIDE
{	
	HudMessage(s:"You are out of vanishing power!"; HUDMSG_FADEOUT, 7, -1, 1.5, 0.65, 3.5, 7.0);
	ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 10);
}

function int hasVanish(int pNumber)
{
	If (PlayerClass(PlayerNumber()) < SEEKER_CLASS_START)
	{
		switch (PlayerClass(PlayerNumber() + 1000))
		{
			case 0:
				if (CheckActorInventory(pNumber + 1000, "Vanishing") == 0)
				{
					return 0;
				}
			case 1:
				if (CheckActorInventory(pNumber + 1000, "lowVanishingPool") == 0)
				{
					return 0;
				}
			case 2:
				if (CheckActorInventory(pNumber + 1000, "medVanishingPool") == 0)
				{
					return 0;
				}
			case 3:
				if (CheckActorInventory(pNumber + 1000, "lowVanishingPool") == 0)
				{
					return 0;
				}
			case 4:
				if (CheckActorInventory(pNumber + 1000, "medVanishingPool") == 0)
				{
					return 0;
				}
			case 5:
				if (CheckActorInventory(pNumber + 1000, "lowVanishingPool") == 0)
				{
					return 0; 
				}
		}
	}
	return 1;
}

Script "VanishHandling" (void) CLIENTSIDE
{
	int vanState;
	int runVarState;
	if(PlayerClass(PlayerNumber()) < SEEKER_CLASS_START) //Hider check
	{
		while (GetActorProperty(PLAYER_BASE_TID + PlayerNumber(), APROP_Health) > 0)
		{
			buttons = GetPlayerInput(-1, INPUT_BUTTONS);
			nobuttons = GetPlayerInput(-1, INPUT_OLDBUTTONS);
				//For people with running on
				if (GetCVar("CL_Run")==1 && isVanished(PLAYER_BASE_TID + PlayerNumber()) == true)
				{
					if (runVarState == 0) { runVarState = 2; }
					if (buttons & BT_SPEED)
					{
						if (nobuttons & BT_SPEED)
							{
								//Continue to be walking invisibly~
								if (runVarState == 1)
								{
									runVarState = 2;
									ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
									//Unless you transition from walking while holding down shift, thena you go invisible.
								}
							}
							else
							{
								if (GetActorProperty(PLAYER_BASE_TID + PlayerNumber(), APROP_Health) > 0)
								{
									vanState = 1;
									ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
									//Become invisible when walking.
								}
							}
					}
					else if (buttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK))
					{
						if (nobuttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK))
						{
							//Stay visible while holding keys down.
							if (vanState == 1)
							{
								vanState = 0;
								ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 9);
								//Become visible if going from walking to running!
							}
							if (runVarState == 1)
							{
								vanState = 0;
								runVarState = 2;
								ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 9);
								//Toggling CL_Run won't get you very far.
							}
						}
						else
						{
							if (GetActorProperty(PLAYER_BASE_TID + PlayerNumber(), APROP_Health) > 0)
							{
								vanState = 0;
								runVarState = 2;
								ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 9);
								//If the player isn't dead and is running around turn visible!
							}
						}
					}
					else 
					{
						if (nobuttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK))
						{
							ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
							//Release the movement keys and you are the invisible man.
						}
						else
						{
							//And do absolutely nothing while you're not moving at all!
						}
					}
				}
				if (GetCVar("CL_Run")==0 && isVanished(PLAYER_BASE_TID + PlayerNumber()) == true)
				//For people with running off (however few there are!)
				{
				if (runVarState == 0) { runVarState = 1; }
					if (buttons & BT_SPEED)
					{
						if (nobuttons & BT_SPEED)
						{
							//Continue to be running around invisible...Unless...
							if (runVarState == 2)
							{
								runVarState = 1;
								ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 9);
								//Unless you transition from running while holding down shift, then you stay visible.
							}
						}
						else
						{
							if (GetActorProperty(PLAYER_BASE_TID + PlayerNumber(), APROP_Health) > 0)
							{
								vanState = 1;
								ACS_NamedExecuteAlways("stateChange", 0, 1, 0, 9);
								//Turn visible while running!
							}
						}
					}
					else if (buttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK))
					{
						if (nobuttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK))
						{
							//Keep walking invisibly when the movement keys are held down.
							if (vanState == 1)
							{
								vanState = 0;
								ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
								//Start walking invisibly if going from running to walking.
							}
							if (runVarState == 2)
							{
								vanState = 0;
								runVarState = 1;
								ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
								//Toggling CL_Run won't get you very far.
							}
						}
						else
						{
							if (GetActorProperty(PLAYER_BASE_TID + PlayerNumber(), APROP_Health) > 0)
							{
								vanState = 0;
								runVarState = 1;
								ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
								//If the player isn't dead and is walking around then don't turn visible.
							}
						}
					}
					else 
					{
						if (nobuttons & (BT_FORWARD | BT_BACK | BT_MOVELEFT | BT_MOVERIGHT | BT_ATTACK | BT_SPEED))
						{
							ACS_NamedExecuteAlways("stateChange", 0, 0, 1, 8);
							//Release the movement keys and you are the invisible man.
						}
						else
						{
							//And do absolutely nothing while you're not moving at all!
						}
					}
				}
				//print(s:"running");
			delay(1);
		}
	}
}

function bool isVanished(int pNumber)
{	
	if (GetActorProperty(pNumber, APROP_Alpha) < 1.0)
	{
		return true;
	}
		return false;
}