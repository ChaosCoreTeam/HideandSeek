ACTOR PowerShrink : PowerMorph
{
	Powerup.Duration 100
    PowerMorph.PlayerClass "ShrunkIllusionist"
	PowerMorph.MorphFlash "SpawnShrinkFlash"
    PowerMorph.UnMorphFlash "SpawnUnShrinkFlash"
}

ACTOR ShrunkIllusionist : IllusionistClass
{
	Radius 4
    Height 14
    Scale 0.25
    PainSound "MiniPain"
    Player.ViewHeight 10
    Player.SoundClass "mini"

    -PICKUP
}


// ------------------------------------------------------------------------------------------------
//
//   [Blue Shadow]
//       Morph flash: all it does is give its target (the player in this case) an item to spawn the
//       real morph flash.
//
// ------------------------------------------------------------------------------------------------

ACTOR ShrinkFlashDummy
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_GiveInventory("SpawnShrinkFlash", 1, AAPTR_TARGET)
        Stop
    }
}

// ------------------------------------------------------------------------------------------------
//
//   [Blue Shadow]
//       Since the player is the spawner of the actual morph flash, their translation can be
//       transferred to the flash, and from the flash to the shrink special effects.
//
// ------------------------------------------------------------------------------------------------

ACTOR SpawnShrinkFlash : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("ShrinkFlash", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        Stop
    }
}

ACTOR ShrinkFlash
{
    +NOINTERACTION

    States
    {
    Spawn:
        TNT1 A 1 NoDelay
        TNT1 A 0 A_PlaySound("misc/p_pkup", CHAN_6)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX1", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX3", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX4", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX5", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX6", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX7", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        Stop
    }
}

// ------------------------------------------------------------------------------------------------
//
//   [Blue Shadow]
//       Unmorph flash: what applies to the morph flash, applies here too.
//
// ------------------------------------------------------------------------------------------------

ACTOR UnShrinkFlashDummy
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_GiveInventory("SpawnUnShrinkFlash", 1, AAPTR_TARGET)
        Stop
    }
}

ACTOR SpawnUnShrinkFlash : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("UnShrinkFlash", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        Stop
    }
}

ACTOR UnShrinkFlash : ShrinkFlash
{
    States
    {
    Spawn:
        TNT1 A 1 NoDelay
        TNT1 A 1 A_SpawnItemEx("ShrinkFX7", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX6", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX5", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX4", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX3", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SpawnItemEx("ShrinkFX1", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        Stop
    }
}

// ------------------------------------------------------------------------------------------------
//
//   [Blue Shadow]
//       Shrink special effects.
//
// ------------------------------------------------------------------------------------------------

ACTOR ShrinkFX1
{
    Scale 0.9

    +NOINTERACTION

    States
    {
    Spawn:
        VSHP A 1 A_FadeOut(0.25)
        Loop
    }
}

ACTOR ShrinkFX2 : ShrinkFX1 { Scale 0.8 }
ACTOR ShrinkFX3 : ShrinkFX1 { Scale 0.7 }
ACTOR ShrinkFX4 : ShrinkFX1 { Scale 0.6 }
ACTOR ShrinkFX5 : ShrinkFX1 { Scale 0.5 }
ACTOR ShrinkFX6 : ShrinkFX1 { Scale 0.4 }
ACTOR ShrinkFX7 : ShrinkFX1 { Scale 0.3 }

