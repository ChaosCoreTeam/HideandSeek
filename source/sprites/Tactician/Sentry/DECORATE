actor SentryGun
{
  obituary "%o was ventilated by an auto-sentry."
  health 250
  radius 20
  height 56
  mass 100000
  speed 0
  MaxTargetRange 2048
  attacksound "sentry/attack"
  MONSTER
  +FLOORCLIP
  +FRIENDLY
  +NOBLOOD
  +LOOKALLAROUND
  +NEVERRESPAWN
  +STANDSTILL
  states
  {
  Spawn:
    SENT AAAAAAAAAAAAAAAA 4 A_Look
    SENT A 0 A_PlaySound ("Sentry/Active")
    loop
  See:
    SENT AAAAAAAAAAAAAAAA 4 A_Chase
    SENT A 0 A_PlaySound ("Sentry/Active")
    loop
  Missile:
    SENT A 16 A_FaceTarget
    SENT B 2 bright A_CPosAttack
    SENT A 2 A_CposRefire
    goto Missile+1
  Death:
    SENT C 1 A_PlaySoundEx ("Sentry/Death", "SoundSlot5")
    SENT C 0 A_PlaySoundEx ("weapons/rocklx", "SoundSlot6")
    SENT C 0 A_SpawnItem ("SentryBoom", 0, 48)
    SENT C 512
  FadeOut:
    SENT C 2 A_FadeOut (0.1)
    loop
  }
}

actor SentryBoom
{
  radius 2
  height 2
  renderstyle Add
  +NOINTERACTION
  +CLIENTSIDEONLY
  states
  {
  Spawn:
    XPL1 ABCDEF 3 bright
    stop
  }
}

ACTOR SentryBox : CustomInventory 12369
{
  +COUNTITEM
  +INVENTORY.INVBAR
  Inventory.MaxAmount 3
  Inventory.Icon "SENBB0"
  Inventory.PickupMessage "You picked up a sentry gun!"
  States
  {
  Spawn:
    SENB A -1
    Stop
  Use:
    TNT1 A 1 A_SpawnItemEx ("SentryGun", 56, 0, 8, 0, 0, 0, 0, 1)
    Stop
  }
}
